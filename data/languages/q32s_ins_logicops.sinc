##################
#
# Logic Operations
#
##################

###########################################################################################
# OR
###########################################################################################

#
# rA |= rB
# => or rA, rB
#
:or regA, regDl is group=5 & ins1212=1 & ins0405=1 & ins0606=1 & ins0708=0 & regA & regDl
{
	regA = regA | regDl;
}

#
# rA = rB | weirdimm
# => or rA, rB, #weirdimm
#
:or regA, regE, #weirdimm is group=7 & ins0412=0x1D0 & regA ; regE & weirdimm
{
	regA = regE | weirdimm;
}

#
# rA = rB | rC
# => or rA, rB, rC
#
:or regE, regF, regG is group=7 & ins0012=0x1DE0 ; regE & regF & regG
{
	regE = regF | regG;
}

###########################################################################################
# XOR
###########################################################################################

#
# rA ^= rB
# => xor rA, rB
#
:xor regA, regDl is group=5 & ins1212=1 & ins0405=1 & ins0606=1 & ins0708=1 & regA & regDl
{
	regA = regA ^ regDl;
}

#
# rA = rB ^ weirdimm
# => xor rA, rB, #weirdimm
#
:xor regA, regE, #weirdimm is group=7 & ins0412=0x1D1 & regA ; regE & weirdimm
{
	regA = regE ^ weirdimm;
}

#
# rA = rB ^ rC
# => xor rA, rB, rC
#
:xor regE, regF, regG is group=7 & ins0012=0x1DE1 ; regE & regF & regG
{
	regE = regF ^ regG;
}

###########################################################################################
# AND
###########################################################################################

#
# rA |= rB
# => and rA, rB
#
:and regA, regDl is group=5 & ins1212=1 & ins0405=1 & ins0606=1 & ins0708=2 & regA & regDl
{
	regA = regA & regDl;
}

#
# rA = rB & weirdimm
# => and rA, rB, #weirdimm
#
:and regA, regE, #weirdimm is group=7 & ins0412=0x1D2 & regA ; regE & weirdimm
{
	regA = regE & weirdimm;
}

#
# rA = rB & rC
# => and rA, rB, rC
#
:and regE, regF, regG is group=7 & ins0012=0x1DE2 ; regE & regF & regG
{
	regE = regF & regG;
}

###########################################################################################
# AND-NOT
###########################################################################################

#
# rA = rB & ~weirdimm
# => andnot rA, rB, #weirdimm
#
:andnot regA, regE, #weirdimm is group=7 & ins0412=0x1D3 & regA ; regE & weirdimm
{
	regA = regE & ~weirdimm;
}

#
# rA = rB & ~rC
# => andnot rA, rB, rC
#
:andnot regE, regF, regG is group=7 & ins0012=0x1DE3 ; regE & regF & regG
{
	regE = regF & ~regG;
}

###########################################################################################
# NOT (one's complement)
###########################################################################################

#
# rA |= rB
# => not rA, rB
#
:not regA, regDl is group=5 & ins1212=1 & ins0405=1 & ins0606=1 & ins0708=3 & regA & regDl
{
	regA = ~regDl;
}

